
\import Proto \import Connection

\func coercion (i k : I) (A : I -> \Type) (x : A i) : A k
   => coe2 A i x k

\func refl {A : \Type} (a : A) : a = a
   => idp

\func rfl {A : \Type} {a : A} : a = a
   => idp

\func HConnAnd {A : \Type} {a b : A} (p : a = b) :
   Path (\lam i => a = p @ i) rfl p
   => path (connAnd p)

\func HConnOr {A : \Type} {a b : A} (p : a = b) :
   Path (\lam i => p @ i = b) p rfl
   | idp => idp

\func trans {A B : \Type} (p : A = B) : A -> B
   => coercion left right (@ p)

\func trans-comp {A : \Type} (a : A) : a = trans (refl A) a
   => rfl

\func transNeg {A B : \Type} (p : A = B) : B -> A
   => coercion right left (@ p)

\func transK {A B : \Type} (p : A = B) (x : A) : x = transNeg p (trans p x)
   => path (\lam i => coercion i left (@ p)
                     (coercion left i (@ p) x))

\func coerce {A B : \Type} (p : A = B) : A -> B
   => trans p

\func transport {A : \Type} (B : A -> \Type) {a b : A} (p : a = b) (x : B a) : B b
   => coe (\lam i => B (p @ i)) x right

\func transport-twist {A : \Type} (B : A -> \Type) {a b : A}
   (p : a = b) (x : B a) : transport B (inv p) (transport B p x) = x
   | _, idp, x => idp

\func comp-pi {A : \Type} (B C : A -> \Type) {a a' : A} (p : a = a') (f : B a -> C a) (x : B a')
   : transport (\lam y => B y -> C y) p f x = transport C p (f (transport B (inv p) x)) \elim p
   | idp => idp

\func comp-path-right {A : \Type} {a0 a b : A} (p : a = b) (q : a0 = a)
   : transport (a0 =) p q = q # p
   | idp, idp => idp

\func comp-inv-func {A : \Type} (B : A -> \Type) {a a' : A} (p : a = a') (g : B a' -> B a) (gf : \Pi (x : B a) -> g (transport B p x) = x) (b : B a')
   : transport B (inv p) b = g b \elim p
   | idp => inv (gf b)

\func comp-# {A : \Type} (B : A -> \Type) {a a' a'' : A} (p : a = a') (q : a' = a'') (x : B a)
   : transport B (p # q) x = transport B q (transport B p x) \elim q
   | idp => idp

\func subst {A : \Type} {B : A -> \Type} {a b : A} (p : a = b) (x : B a) : B b
   | idp, x => x

\func HPath {A B : \Type} (a : A) (b : B) (P : A = B) : \Type
   => trans P a = b

\func singl (A : \Type) (a : A) : \Type
   => \Sigma (x : A) (a = x)

\func theta (A : \Type) (a : A) : singl A a
   => (a, refl a)

\func inv {A : \Type} {a b : A} (p : a = b) : b = a
   | idp => idp

\func seg : left = right => path idfun

\func negate (i : I) : I => inv seg @ i

\func \infixl 5 # {A : \Type} {a b c : A} (p : a = b) (q : b = c) : a = c
   | p, idp => p  \where
   \func concatOverFamily {A : I -> \Type} {a : A left} {a' a'' : A right} (p : Path A a a') (q : a' = a'') : Path A a a'' =>
      transport (Path A a) q p

\func kan {A : \Type} {a b c d : A} (bottom : a = b)
   (left : a = c) (right : b = d) : c = d
   | idp, idp, idp => idp

{-       u
    a ------> b
    |         |
  p |         | q
    |         |
    V         V
    c ------> d
         v       -}
\func Square {A : \Type} {a b c d : A}
   (u : a = b) (v : c = d) (p : a = c) (q : b = d)
   => Path (\lam i => u @ i = v @ i) p q

\func pathOverFamily {A : I -> \Type} {a : A left} {a' : A right} (p : coe A a right = a') : Path A a a' =>
   #.concatOverFamily (path (coe A a)) p

-- Groupoid Interpretation

\func refl-left {A : \Type} {a b : A} (p : a = b) : rfl # p = p
   => J (\lam _ p => rfl # p = p) rfl p

\func refl-right {A : \Type} {a b : A} (p : a = b) : p # rfl = p
   => coercion left right (\lam i => connAnd p i # rfl = connAnd p i) rfl

\func comp-path-inv {A : \Type} {a b : A} (p : a = b) : p # inv p = rfl
   => coercion left right (\lam i => connAnd p i # inv (connAnd p i) = rfl)
      rfl

\func comp-inv-path {A : \Type} {a b : A} (p : a = b) : inv p # p = rfl
   => coercion left right (\lam i => inv (connAnd p i) # connAnd p i = rfl)
      rfl

\func cong {A B : \Type} (f : A -> B) {a b : A} (p : a = b) : f a = f b
   | _, idp => idp

\func contr {A : \Type} {a b : A} (p : a = b) : theta A a = (b, p)
   | idp => idp

\func coe-path {A : \Type} {a1 a2 a3 a4 : A} (p : a2 = a1) (q : a2 = a3) (r : a3 = a4)
   : coe (\lam i => p @ i = r @ i) q right = (inv p # (q # r))
   | idp, idp, idp => idp

\func D (A : \Type) : \Type => \Pi (x y : A) -> x = y -> \Type

\func J {A : \Type} {a : A} (C : \Pi (x : A) -> a = x -> \Type)
        (d : C a (refl a)) {x : A} (p : a = x) : C x p
-- => coe (\lam i => C (p @ i) (connAnd p i)) d right -- normal form
   => transport (\lam x => C x.1 x.2) (contr p) d

\func inv-inv {A : \Type} {a a' : A} (p : a = a') : inv (inv p) = p
   | idp => idp

\func comp-assoc {A : \Type} {a b c d : A}
   (p : a = b) (q : b = c) (r : c = d) : p # (q # r) = (p # q) # r
   | idp, idp, idp => idp

\func inv-J {A : \Type} {a b : A} (p : a = b) : b = a
   => J (\lam x _ => x = a) rfl p

\func n-groupoid (A : \Type) (n : Nat) : \Type \elim n
   | zero => \Pi (a b : A) -> a = b
   | suc n => \Pi (a b : A) -> n-groupoid (a = b) n

\func isContr     (A : \Type) : \Type => \Sigma (x : A) (\Pi (y : A) -> x = y)
\func isProp      (A : \Type) : \Type => n-groupoid A 0
\func isSet       (A : \Type) : \Type => n-groupoid A 1
\func isGroupoid  (A : \Type) : \Type => n-groupoid A 2
\func isGrp2      (A : \Type) : \Type => n-groupoid A 3
\func isGrp3      (A : \Type) : \Type => n-groupoid A 4

\func Prop     : \Type => \Sigma (X : \Type) (isProp X)
\func Set      : \Type => \Sigma (X : \Type) (isSet X)
\func Groupoid : \Type => \Sigma (X : \Type) (isGroupoid X)

\func K (A : \Type) => \Pi {a : A} (p : a = a) -> p = rfl

\func homotopy {A : \Type} (C : A -> \Type) (f g : \Pi (x : A) -> C x)
   => \Pi (x : A) -> f x = g x

\func funext {A : \Type} (C : A -> \Type) {f g : \Pi (x : A) -> C x}
   (p : homotopy C f g) : f = g
   => path (\lam i x => (p x) @ i)

\func funext-nondep {A B : \Type} {f g : A -> B} :
   homotopy (\lam _ => B) f g -> f = g
   => funext (\lam _ => B)

{-
\func I-ind (C : I -> \Type) {b0 : C left} {b1 : C right} (s : Path C b0 b1)
   (i : I) : C i \elim i
   | left => b0
   | right => b1
   | i => s @ i
-}

\func I-contr-left (x : I) : left = x
   => coercion left x (\lam x => left = x) rfl

\func I-contr-right (x : I) : right = x
   => coercion right x (\lam x => right = x) rfl

\data No
   | implicit I
   \where {
      \func i0 => implicit left
      \func i1 => implicit right
      \func seg : i0 = i1 => path implicit
   }
